func.func @tcolexpand_kernel_2d(%arg0: memref<1024x1024xi32, #pto.address_space<gm>>,
                                %arg1: memref<1024x1024xi32, #pto.address_space<gm>>,
                                %arg2: index, %arg3: index)
  attributes {hacc.entry, pto.func_core_type = #pto.func_core_type<AIV>} {
  // ==========================================
  // 1. 计算内存偏移量 (32x32 tile)
  // ==========================================
  %c0_i64 = arith.constant 0 : i64
  %c4096_i64 = arith.constant 4096 : i64
  %row = affine.apply affine_map<()[s0] -> (s0 * 32)>()[%arg2]
  %col = affine.apply affine_map<()[s0] -> (s0 * 32)>()[%arg3]
  // ==========================================
  // 2. 创建 Subview (32x32 窗口)
  // ==========================================
  %in_gm = memref.subview %arg0[%row, %col] [32, 32] [1, 1]
    : memref<1024x1024xi32, #pto.address_space<gm>>
      to memref<32x32xi32, strided<[1024, 1], offset: ?>, #pto.address_space<gm>>
  %out_gm = memref.subview %arg1[%row, %col] [32, 32] [1, 1]
    : memref<1024x1024xi32, #pto.address_space<gm>>
      to memref<32x32xi32, strided<[1024, 1], offset: ?>, #pto.address_space<gm>>
  // ==========================================
  // 3. Assign pointer (UB: 32x32 = 1024 elements)
  // ==========================================
  %ub_src = pto.pointer_cast(%c0_i64) : memref<1024xi32, #pto.address_space<ub>>
  %ub_dst = pto.pointer_cast(%c4096_i64) : memref<1024xi32, #pto.address_space<ub>>
  // ==========================================
  // 4. DMA Load GM -> UB
  // ==========================================
  pto.tload ins(%in_gm
      : memref<32x32xi32, strided<[1024, 1], offset: ?>, #pto.address_space<gm>>)
    outs(%ub_src : memref<1024xi32, #pto.address_space<ub>>)
  // ==========================================
  // 5. Compute: column expand (broadcast src[0, j] to all rows in column j)
  // ==========================================
  pto.set_flag[<PIPE_MTE2>, <PIPE_V>, <EVENT_ID0>]
  pto.wait_flag[<PIPE_MTE2>, <PIPE_V>, <EVENT_ID0>]
  pto.tcolexpand_dps ins(%ub_src : memref<1024xi32, #pto.address_space<ub>>)
                    outs(%ub_dst : memref<1024xi32, #pto.address_space<ub>>)
  // ==========================================
  // 6. DMA Store UB -> GM
  // ==========================================
  pto.set_flag[<PIPE_V>, <PIPE_MTE3>, <EVENT_ID0>]
  pto.wait_flag[<PIPE_V>, <PIPE_MTE3>, <EVENT_ID0>]
  pto.store_dps ins(%ub_dst : memref<1024xi32, #pto.address_space<ub>>)
    outs(%out_gm
      : memref<32x32xi32, strided<[1024, 1], offset: ?>, #pto.address_space<gm>>)
  return
}