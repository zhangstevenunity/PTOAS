func.func @tilescalar_add_kernel_2d(%arg0: memref<1024x1024xi32, #pto.address_space<gm>>,
                               %arg1: memref<1024x1024xi32, #pto.address_space<gm>>,
                               %arg2: i32,  // scalar
                               %arg3: index, %arg4: index)
  attributes {hacc.entry, pto.func_core_type = #pto.func_core_type<AIV>} {
  // ==========================================
  // 1. 计算内存偏移量 (32x32 tile)
  // ==========================================
  %c4096_i64 = arith.constant 4096 : i64
  %c0_i64 = arith.constant 0 : i64
  %0 = affine.apply affine_map<()[s0] -> (s0 * 32)>()[%arg3]
  %1 = affine.apply affine_map<()[s0] -> (s0 * 32)>()[%arg4]
  // ==========================================
  // 2. 创建 Subview (32x32 窗口)
  // ==========================================
  %subview_in = memref.subview %arg0[%0, %1] [32, 32] [1, 1]
    : memref<1024x1024xi32, #pto.address_space<gm>>
      to memref<32x32xi32, strided<[1024, 1], offset: ?>, #pto.address_space<gm>>
  %subview_out = memref.subview %arg1[%0, %1] [32, 32] [1, 1]
    : memref<1024x1024xi32, #pto.address_space<gm>>
      to memref<32x32xi32, strided<[1024, 1], offset: ?>, #pto.address_space<gm>>
  // ==========================================
  // 3. Assign pointer (UB: 32x32 = 1024 elements)
  // ==========================================
  %ub_in  = pto.pointer_cast(%c0_i64) : memref<1024xi32, #pto.address_space<ub>>
  %ub_out = pto.pointer_cast(%c4096_i64) : memref<1024xi32, #pto.address_space<ub>>
  // ==========================================
  // 4. DMA Load
  // ==========================================
  pto.tload ins(%subview_in
      : memref<32x32xi32, strided<[1024, 1], offset: ?>, #pto.address_space<gm>>)
    outs(%ub_in : memref<1024xi32, #pto.address_space<ub>>)
  // ==========================================
  // 5. Compute: elementwise add scalar
  //    tadds_dps ins(%src, %scalar : type(src), type(scalar)) outs(%dst : type(dst))
  // ==========================================
  pto.set_flag[<PIPE_MTE2>, <PIPE_V>, <EVENT_ID0>]
  pto.wait_flag[<PIPE_MTE2>, <PIPE_V>, <EVENT_ID0>]
  pto.tadds_dps ins(%ub_in, %arg2 : memref<1024xi32, #pto.address_space<ub>>, i32)
            outs(%ub_out : memref<1024xi32, #pto.address_space<ub>>)
  // ==========================================
  // 6. DMA Store
  // ==========================================
  pto.set_flag[<PIPE_V>, <PIPE_MTE3>, <EVENT_ID0>]
  pto.wait_flag[<PIPE_V>, <PIPE_MTE3>, <EVENT_ID0>]
  pto.store_dps ins(%ub_out : memref<1024xi32, #pto.address_space<ub>>)
    outs(%subview_out
      : memref<32x32xi32, strided<[1024, 1], offset: ?>, #pto.address_space<gm>>)
  return
}