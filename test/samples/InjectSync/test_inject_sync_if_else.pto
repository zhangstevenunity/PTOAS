// RUN: ./bin/ptoas --enable-insert-sync %s 2>&1 1>/dev/null | FileCheck %s

module {
  // CHECK-LABEL: func.func @test_if_else_sync
  // CHECK: pto.tload
  // CHECK: pto.set_flag[<PIPE_MTE2>, <PIPE_V>, <EVENT_ID0>]
  // CHECK: pto.wait_flag[<PIPE_MTE2>, <PIPE_V>, <EVENT_ID0>]
  // CHECK: scf.if
  // CHECK: pto.set_flag[<PIPE_V>, <PIPE_MTE3>, <EVENT_ID0>]
  // CHECK: pto.wait_flag[<PIPE_V>, <PIPE_MTE3>, <EVENT_ID0>]
  // CHECK: pto.tstore
  // CHECK: pto.barrier <PIPE_ALL>
  func.func @test_if_else_sync(%arg0: memref<16x16x16xf16, #pto.address_space<gm>>,
                               %arg1: memref<16x16x16xf16, #pto.address_space<gm>>,
                               %cond: i1) {
    %ub0 = memref.alloc() : memref<16x16x16xf16, #pto.address_space<vec>>
    %ub1 = memref.alloc() : memref<16x16x16xf16, #pto.address_space<vec>>
    pto.tload ins(%arg0 : memref<16x16x16xf16, #pto.address_space<gm>>)
                 outs(%ub0 : memref<16x16x16xf16, #pto.address_space<vec>>)
    scf.if %cond {
      pto.tadd ins(%ub0, %ub0 : memref<16x16x16xf16, #pto.address_space<vec>>,
                                   memref<16x16x16xf16, #pto.address_space<vec>>)
                   outs(%ub1 : memref<16x16x16xf16, #pto.address_space<vec>>)
    } else {
      pto.sub_dps ins(%ub0, %ub0 : memref<16x16x16xf16, #pto.address_space<vec>>,
                                  memref<16x16x16xf16, #pto.address_space<vec>>)
                  outs(%ub1 : memref<16x16x16xf16, #pto.address_space<vec>>)
    }
    pto.tstore ins(%ub1 : memref<16x16x16xf16, #pto.address_space<vec>>)
                  outs(%arg1 : memref<16x16x16xf16, #pto.address_space<gm>>)
    return
  }
}
